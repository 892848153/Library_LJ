if(isBuildModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
    apply from: 'tinker-support.gradle'
}
apply plugin: 'realm-android'
apply plugin: 'AndResGuard'
apply plugin: 'bugly'
apply plugin: 'packer'

bugly {
    appId = 'b1e580cd5e' // 注册时分配的App ID
    appKey = '1548eece-4f5f-478d-a23e-b75944144286' // 注册时分配的App Key
}

android {
    compileSdkVersion rootProject.ext.COMPILE_SDK_VERSION
    buildToolsVersion rootProject.ext.BUILD_TOOLS_VERSION
//    resourcePrefix "new_house_"
    defaultConfig {
        applicationId "com.lj.library"
        minSdkVersion rootProject.ext.MIN_SDK_VERSION
        targetSdkVersion rootProject.ext.TARGET_SDK_VERSION
        versionCode rootProject.ext.VERSION_CODE
        versionName rootProject.ext.VERSION_NAME
        multiDexEnabled true
    }
    sourceSets {
        main {
            if (isBuildModule.toBoolean()) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/release/AndroidManifest.xml'
            }
        }
    }

    dexOptions {
        jumboMode true
    }

    signingConfigs {
        releaseConfig {
            keyAlias 'lj'
            keyPassword '2021516'
            storeFile file('/Users/liujie_gyh/Documents/ljkeystore')
            storePassword '2021516'
            v2SigningEnabled false
        }
    }

    buildTypes {
        release {
            minifyEnabled true //启动proguard
            shrinkResources true // 编译阶段去除无用的资源
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'x86'
            universalApk false
        }
    }

    //    android6.0中删除apache包, 用此引用
    useLibrary 'org.apache.http.legacy'
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    // 解决lint检查出错误导致打包失败的问题
    lintOptions {
        abortOnError false
    }
    // 启动DataBinding
    dataBinding {
        enabled = true
    }
    aaptOptions {
        //设置为false的话，编译打包时aapt将不会使用libpng, libjpeg对图片进行压缩
        //需要你自己使用工具压缩图片,比如pngquant, tinypng
        cruncherEnabled true
    }
}

// 将第三库引用的android support库的版本都改成统一的版本号
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex") && !requested.name.startsWith("constraint")) {
                details.useVersion "${APPCOMPAT_VERSION}"
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //log日志
    implementation 'com.orhanobut:logger:2.2.0'

    //butterknife
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    //okhttp + retrofit + rxandroid
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation "com.squareup.okhttp3:okhttp:${OKHTTP_VERSION}"
    implementation "com.squareup.okhttp3:logging-interceptor:${OKHTTP_VERSION}"
    implementation "com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}"
    implementation "com.squareup.retrofit2:converter-gson:${RETROFIT_VERSION}"
    implementation "com.squareup.retrofit2:adapter-rxjava:${RETROFIT_VERSION}"

    //leakcanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${LEAK_CANARY_VERSION}"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${LEAK_CANARY_VERSION}"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${LEAK_CANARY_VERSION}"

    //support 兼容包
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation "com.android.support:design:${APPCOMPAT_VERSION}"
    implementation "com.android.support:support-v4:${APPCOMPAT_VERSION}"
    implementation "com.android.support:appcompat-v7:${APPCOMPAT_VERSION}"
    implementation "com.android.support:recyclerview-v7:${APPCOMPAT_VERSION}"

    //dagger2
    implementation 'com.google.dagger:dagger:2.8'
    //android-apt插件-引入dagger-compiler包, 不会编译到最后的源码中
    annotationProcessor 'com.google.dagger:dagger-compiler:2.8'

    //单元测试-引入junit包, 会自动引入hamcrest包
    testImplementation 'junit:junit:4.12'

    //Mock方式的包mockito
    testImplementation 'org.mockito:mockito-core:2.0.76-beta'

    //google的protobuf运行时环境
    implementation 'com.google.protobuf:protobuf-java:3.4.0'

    //square公司出的protobuf方案"wire"的运行时环境
    implementation 'com.squareup.wire:wire-runtime:2.2.0'

    //fresco加载图片
    implementation 'com.facebook.fresco:fresco:1.8.1'

    //facebook的调优工具stecho
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    // bugly升级SDK已经集成crash上报功能
    implementation 'com.tencent.bugly:crashreport_upgrade:latest.release'

    //其中latest.release指代最新Bugly NDK版本号，也可以指定明确的版本号，例如3.0
    implementation 'com.tencent.bugly:nativecrashreport:latest.release'

    //渠道打包工具
    implementation 'com.mcxiaoke.gradle:packer-helper:1.0.9'
}

// AndResGuard 配置开始----------------------------------
andResGuard {
    // mappingFile = file("./resource_mapping.txt")
    mappingFile = null
    // 当你使用v2签名的时候，7zip压缩是无法生效的。
    use7zip = true
    useSign = true
    // 打开这个开关，会keep住所有资源的原始路径，只混淆资源的名字
    keepRoot = false
    whiteList = [
            // for your icon
            "R.drawable.icon",
            // for fabric
            "R.string.com.crashlytics.*",
            // for umeng update
            "R.string.umeng*",
            "R.string.UM*",
            "R.string.tb_*",
            "R.layout.umeng*",
            "R.layout.tb_*",
            "R.drawable.umeng*",
            "R.drawable.tb_*",
            "R.anim.umeng*",
            "R.color.umeng*",
            "R.color.tb_*",
            "R.style.*UM*",
            "R.style.umeng*",
            "R.id.umeng*",
            // umeng share for sina
            "R.drawable.sina*",
            // for google-services.json
            "R.string.google_app_id",
            "R.string.gcm_defaultSenderId",
            "R.string.default_web_client_id",
            "R.string.ga_trackingId",
            "R.string.firebase_database_url",
            "R.string.google_api_key",
            "R.string.google_crash_reporting_api_key",
            // umeng share for facebook
            "R.layout.*facebook*",
            "R.id.*facebook*",
            // umeng share for messager
            "R.layout.*messager*",
            "R.id.*messager*",
            // umeng share commond
            "R.id.progress_bar_parent",
            "R.id.webView"
    ]
    compressFilePattern = [
            "*.png",
            "*.jpg",
            "*.jpeg",
            "*.gif",
            "resources.arsc"
    ]
    sevenzip {
        artifact = 'com.tencent.mm:SevenZip:1.2.12'
        //path = "/usr/local/bin/7za"
    }
}
// AndResGuard 配置结束---------------------------------
